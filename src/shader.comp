#version 430

layout (local_size_x = 128) in;

const int N = 8;

#define Element uint[N]

layout(binding = 0) uniform bufParams {
    uint IterCount;
};

layout(binding = 1) buffer  readonly bufA { Element A[]; };
layout(binding = 2) buffer  readonly bufB { Element B[]; };
layout(binding = 3) buffer  bufR          { Element R[]; };
layout(binding = 4) buffer  bufTemp       { uint    t[]; };

const Element Fq_rawq = {0xd87cfd47,0x3c208c16,0x6871ca8d,0x97816a91,0x8181585d,0xb85045b6,0xe131a029,0x30644e72};
const uint    Fq_np = 0xe4866389;

#define AssignElement(res, arg, size)     \
    for (int i = 0; i < size; i++) {res[i] = arg[i];}

int mp_cmp(Element a, Element b)
{
    for (int i = N - 1; i >= 0; i--) {
        if (a[i] > b[i]) return 1;
        if (a[i] < b[i]) return -1;
    }
    return 0;
}

uint mp_sub_n(out Element r, Element a, Element b)
{
    uint borrow = 0;

    for (int i = 0; i < N; i++) {
        uint borrow2 = 0;

        r[i] = usubBorrow(a[i], borrow, borrow);
        r[i] = usubBorrow(r[i], b[i], borrow2);
        borrow += borrow2;
    }
    return borrow;
}

uint mp_add_n(out Element r, Element a, Element b)
{
    uint carry = 0;

    for (int i = 0; i < N; i++) {
        uint carry2 = 0;

        r[i] = uaddCarry(a[i], carry, carry);
        r[i] = uaddCarry(r[i], b[i], carry2);
        carry += carry2;
    }
    return carry;
}

void mp_addmul_1(inout Element r, Element a, uint b, inout uint last)
{
    uint carry = 0;

    for (int i = 0; i < N; i++) {
        uint hi, lo;

        umulExtended(a[i], b, hi, lo);

        lo   = uaddCarry(lo, carry, carry);
        r[i] = uaddCarry(r[i], lo, lo);

        carry += lo + hi;
    }

    last = uaddCarry(last, carry, carry);
}

void Fq_rawAdd(out Element r, Element a, Element b)
{
    uint carry = mp_add_n(r, a, b);

    if (carry != 0 || mp_cmp(r, Fq_rawq) >= 0) {
        mp_sub_n(r, r, Fq_rawq);
    }
}

void Fq_rawMMul(out Element r, Element a, Element b)
{
    r = Element(0,0,0,0,0,0,0,0);

    for (int i = 0; i < N; i++) {
        uint last = 0;

        mp_addmul_1(r, b, a[i], last);
        mp_addmul_1(r, Fq_rawq, r[0] * Fq_np, last);

        for (int j = 0; j < N-1; j++) {
            r[j] = r[j+1];
        }
        r[N-1] = last;
        last = 0;
    }

    if (mp_cmp(r, Fq_rawq) >= 0) {
        mp_sub_n(r, r, Fq_rawq);
    }
}

void main() {
    uint x = gl_GlobalInvocationID.x;

    const Element a = A[x];
    const Element b = B[x];
    Element r;

    for (int i = 0; i < IterCount; i++) {
        Fq_rawAdd(r, a, b);
        Fq_rawMMul(r, a, b);
    }

    AssignElement(R[x], r, N);
}
